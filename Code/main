Ship ship;
Fleet fleet;
Mothership mothership;
ArrayList<Bullet> bullets = new ArrayList <Bullet>();
ArrayList<Bomb> bombs = new ArrayList <Bomb>();
int score = 0;
int lives = 3;
String state = "play";

void setup() {
  //frameRate(30);
  size(1000, 800);
  initialiseGame();
}

void draw() {
  if (state.equalsIgnoreCase("play")) {    
    if (isGameOver()) {
      state = "game_over";
    }
    background(0);
    displayInfoBar();
    drawBullets(); // draws the players bullets
    drawBombs(); // draws the dropped bombs

    fleet.display();
    if (frameCount%20 == 0) {
      fleet.dropBomb();
    }

    ship.display();
    ship.move(); // enables ship movement
    ship.shoot(); // enables ship to shoot
    if (frameCount%20 == 0) {
      ship.canShoot = true;
    }
    if (mothership != null) {
      mothership.display();
      if (mothership.collide()) {
        score += 100;
        mothership = null;
      }
    }
    if (frameCount%200 == 0) {
      generateMothership();
    }
  } else if (state.equalsIgnoreCase("game_over")) {
    gameOver();
  }
}

void displayInfoBar() {
  fill(255);
  rectMode(CORNER);
  rect(0, 0, width, 30);
  textSize(24);
  fill(0);
  text("Score: " + score, 10, 25);
  text("Lives: " + lives, 240, 25);
}

void drawBullets() { // bullets player fire
  for (int i = 0; i < bullets.size(); i++) {
    Bullet b = bullets.get(i);
    b.display();
    // deleting bullets arraylist once bullet disappears off screen
    if (b.y + b.ht/2 < 0) {
      bullets.remove(i);
    }
  }
}

void drawBombs() { // bombs invaders drop
  for (int i = 0; i < bombs.size(); i++) {
    Bomb b = bombs.get(i);
    b.display();
    // deleting bombs arraylist once bullet disappears off screen
    if (b.y > height) {
      bombs.remove(i);
    }
  }
}

void generateMothership() {
  if (mothership == null) {
    if (Math.random() > 0.9) {
      int perk = (int)(random(5));
      mothership = new Mothership(perk);
    }
  }
}

boolean isGameOver() {
  if (lives < 1) {
    return true;
  }
  return false;
}

void gameOver() {
  // sets all objects to null
  ship = null;
  mothership = null;
  fleet = null;

  background(0);
  textSize(100);
  fill(255);
  text("Game Over!", 50, 225);
}

// ------- initialisation -------
void initialiseGame() {
  bullets.clear();
  bombs.clear();
  ship = new Ship(width/2);
  fleet = new Fleet();
  fleet.create();
}
