Ship ship;
Fleet fleet;
Mothership mothership;
ArrayList<Bullet> bullets = new ArrayList <Bullet>();
ArrayList<Bomb> bombs = new ArrayList <Bomb>();
int score;
int lives;
PImage title;
PImage loading;
PFont customFont;
String state;
int startTime;

void setup() {
  //frameRate(30);
  size(1000, 800);
  customFont = createFont("ca.ttf", 32); // loading custom font
  title = loadImage("sprites/title.png");
  loading = loadImage("sprites/loading.jpg");
  state = "menu";
}

void draw() {
  background(0);

  if (state.equalsIgnoreCase("menu")) {
    displayMenu();
  }

  if (state.equalsIgnoreCase("loading")) {  
    displayLoading();
  }

  if (state.equalsIgnoreCase("game_over")) {
    gameOver();
  }

  if (state.equalsIgnoreCase("play")) {    
    if (isGameOver()) {
      textFont(customFont);
      state = "game_over";
    }
    displayInfoBar();
    drawBullets(); // draws the players bullets
    drawBombs(); // draws the dropped bombs

    fleet.display();
    if (frameCount%20 == 0) {
      fleet.dropBomb();
    }

    ship.display();
    ship.move(); // enables ship movement
    ship.shoot(); // enables ship to shoot
    if (frameCount%20 == 0) {
      ship.canShoot = true;
    }
    if (mothership != null) {
      mothership.display();
      if (mothership.collide()) {
        score += 100;
        mothership = null;
      }
    }
    if (frameCount%200 == 0) {
      generateMothership();
    }
  }
}

// ------- METHODS FOR PLAYING GAME -------

void displayInfoBar() {
  fill(255);
  rectMode(CORNER);
  rect(0, 0, width, 30);
  textFont(customFont);
  textSize(24);
  textAlign(CORNER);
  fill(0);
  text("Score <" + score + ">", 10, 25);
  text("Lives <" + lives + ">", 240, 25);
}

void drawBullets() { // bullets player fire
  for (int i = 0; i < bullets.size(); i++) {
    Bullet b = bullets.get(i);
    b.display();
    // deleting bullets arraylist once bullet disappears off screen
    if (b.y + b.ht/2 < 0) {
      bullets.remove(i);
    }
  }
}

void drawBombs() { // bombs invaders drop
  for (int i = 0; i < bombs.size(); i++) {
    Bomb b = bombs.get(i);
    b.display();
    // deleting bombs arraylist once bullet disappears off screen
    if (b.y > height) {
      bombs.remove(i);
    }
  }
}

void generateMothership() {
  if (mothership == null) {
    //if (Math.random() > 0.9) {
    int perk = (int)(random(5));
    mothership = new Mothership(perk);
    //}
  }
}

boolean isGameOver() {
  if (lives < 1) {
    return true;
  }
  return false;
}

// ------- GAME OVER SCREEN -------

void gameOver() {
  // sets all objects to null
  ship = null;
  mothership = null;
  fleet = null;

  textSize(100);
  fill(255);
  textAlign(CENTER, CENTER);
  text("Game Over!", width/2, height/2);
  textSize(20);
  text ("Press space to restart", width/2, 2*height/3);
}

// ------- MENU -------

void displayMenu() {
  imageMode(CENTER);
  image(title, width/2, height/4);
  textFont(customFont);
  textAlign(CENTER, CENTER);
  textSize(28);
  text("Controls \nSpace : Shoot\nMove left/right : Arrow Keys", width/2, 3*height/5);
  textSize(45);
  text("PRESS SPACE TO START GAME", width/2, 5*height/6);
}

// ------- LOADING SCREEN --------

void displayLoading() {
  initialiseGame();
  imageMode(CENTER);
  image(loading, width/2, height/2);
  if (millis() > startTime + 2500) {
    state = "play";
  }
}

// ------- INITIALISATION -------

void initialiseGame() {
  bullets.clear();
  bombs.clear();
  ship = new Ship(width/2);
  fleet = new Fleet();
  fleet.create();
  lives = 3;
  score = 0;
}
